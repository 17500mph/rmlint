#!/usr/bin/env python
# encoding: utf-8

Import('env')
Import('VERSION_MAJOR')
Import('VERSION_MINOR')
Import('VERSION_PATCH')
Import('create_uninstall_target')

import os
import codecs


def build_config_template(target, source, env):
    text = source[0].get_text_contents()

    with codecs.open(target[0].get_abspath(), 'w') as handle:
        handle.write(text.format(
            INSTALL_PREFIX=GetOption('actual_prefix') or GetOption('prefix'),
            HAVE_GLIB=int(env['glib']),
            HAVE_LIBINTL=int(WhereIs('msgfmt') is not None),
            HAVE_BLKID=int(env['blkid']),
            VERSION_MAJOR=VERSION_MAJOR,
            VERSION_MINOR=VERSION_MINOR,
            VERSION_PATCH=VERSION_PATCH,
            VERSION_GIT_REVISION=env['gitrev']
        ))

def build_python_formatter(target, source, env):
    text = source[0].get_text_contents()

    with codecs.open('src/formats/py.py', 'r') as handle:
        py_source = handle.read()

    # Prepare the Python source to be compatible with C-strings
    py_source = py_source.replace('"', '\\"')
    py_source = '\\n"\n"'.join(py_source.splitlines())

    with codecs.open(target[0].get_abspath(), 'w') as handle:
        handle.write(text.replace('<<PYTHON_SOURCE>>', py_source))


config = env.Command(
    'config.h', 'config.h.in', build_config_template
)

py_formatter = env.Command(
    'formats/py.c', 'formats/py.c.in', build_python_formatter
)

program = env.Program(
    '../rmlint',
    Glob('*.c') +
    Glob('checksums/*.c') +
    Glob('formats/*.c') +
    Glob('libart/*.c')
)

env.Depends(program, [config, py_formatter])

if 'install' in COMMAND_LINE_TARGETS:
    env.Alias('install', env.Install('$PREFIX/bin', [program]))


if 'uninstall' in COMMAND_LINE_TARGETS:
    create_uninstall_target(env, "$PREFIX/bin/rmlint")

Return('program')
